import com.amazonaws.services.lambda.model.Runtime
import jp.classmethod.aws.gradle.lambda.AWSLambdaMigrateFunctionTask

buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "jp.classmethod.aws:gradle-aws-plugin:0.22"
    }
}

plugins {
    id "org.sonarqube" version "2.7"
}


apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'jp.classmethod.aws.lambda'
jacoco {
    toolVersion = '0.8.1'
}

group = 'com.ioet'
version = '0.0.1'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    springCloudVersion = 'Finchley.SR1'
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile 'io.springfox:springfox-swagger2:2.9.2'
    compile 'io.springfox:springfox-swagger-ui:2.9.2'
    compile 'com.github.derjust:spring-data-dynamodb:5.0.3'
    compileOnly('org.projectlombok:lombok')
    annotationProcessor 'org.projectlombok:lombok'
    testCompile('org.springframework.boot:spring-boot-starter-test')
    compile group: 'net.sf.dozer', name: 'dozer', version: '5.5.1'
    compile 'com.amazonaws:aws-lambda-java-core:1.1.0'
    compile 'com.amazonaws:aws-lambda-java-events:1.1.0'
    compile group: 'com.amazonaws.serverless', name: 'aws-serverless-java-container-core', version: '1.3'
    compile group: 'com.amazonaws.serverless', name: 'aws-serverless-java-container-spring', version: '1.3'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    dependsOn 'cleanTest'
    jacoco {
        excludes += [
                '**/config/**',
                '**/BpmPeopleApiApplication.class',
                '**/handlers/PeopleApiLambdaHandler'
        ]
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    jacocoTestReport {
        afterEvaluate {
            classDirectories = files(classDirectories.files.collect {
                fileTree(dir: it, exclude: ['**/config/**', '**/BpmPeopleApiApplication.class',
                                            '**/handlers/PeopleApiLambdaHandler.class'])
            })
        }
    }
}

check.dependsOn jacocoTestReport

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

build.dependsOn buildZip

// Gradle AWS Plugin
aws {
    profileName = "default"
    region = "us-east-1"
}

task deploy(type: AWSLambdaMigrateFunctionTask, dependsOn: build) {
    functionName = "people-api"
    handler = "com.ioet.bpm.people.handlers.PeopleApiLambdaHandler::handleRequest"
    role = "arn:aws:iam::524819651720:role/API-lambda-deployment"
    runtime = Runtime.Java8
    zipFile = buildZip.archivePath
    timeout = 50
}

